(* this example is taken from the slides given at Bamberg, summer 2019 *)
(* It is the prototypical example of a cyclic circuit that is constructively *)
(* causal (but not causal in the Lustre/Lucid Synchrone/Zelus sense *)
let node mux(c, x, y) returns (o)
  if c then do o = x done else do o = y done

let node f(x) = x + 1
let node g(x) = x - 1
    
let node constructive(c, x) returns (y)
  local x1, x2, y1, y2
  do x1 = mux(c, x, y2)
  and x2 = mux(c, y1, x)
  and y1 = f(x1)
  and y2 = g(x2)
  and y = mux(c, y2, y1)

let node main() returns (y)
  local c, x
  do  y = constructive(c, x)
  and
      c = true fby true fby not c
  and
      x = 0 -> pre x + 1
  done
      
